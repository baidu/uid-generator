buildscript {
    repositories {
        maven { url 'https://artifactory.platform.paypay.ne.jp/artifactory/libs-release/' }
    }
    dependencies {
        classpath 'jp.ne.paypay.gradle:paypay-gradle-plugin:2.5.6'
        classpath 'jp.ne.paypay.gradle:paypay-dependency-management-plugin:2.6.6-PP4'
    }
}

plugins {
    id 'java-library'
    id 'org.flywaydb.flyway' version "6.3.2"
}

apply plugin: 'paypay-dependency-management'
apply plugin: 'paypay'


group = 'com.baidu.fsg'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

paypay {
    // Plugin documentation https://github.com/Pay-Baymax/paypay-gradle-plugin
    ci {
        projectName = 'uid-generator'
        githubRepoName = 'paypay/uid-generator'
        publishLibrary = true
    }
}

repositories {
    maven { url 'https://artifactory.platform.paypay.ne.jp/artifactory/libs-release/' }
}


configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {

    ext {
        springVersion = '5.3.18'
    }

    implementation "org.springframework:spring-core:${springVersion}"
    implementation "org.springframework:spring-beans:${springVersion}"
    implementation "org.springframework:spring-context:${springVersion}"
    implementation "org.springframework:spring-jdbc:${springVersion}"
    implementation 'org.mybatis:mybatis:3.5.9'
    implementation 'org.mybatis:mybatis-spring:2.0.7'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.slf4j:slf4j-api:1.7.7'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.7'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation "org.springframework:spring-test:${springVersion}"
    testImplementation 'mysql:mysql-connector-java:8.0.29'
    testImplementation 'com.alibaba:druid:1.2.9'
}

java {
    withSourcesJar()
}

flyway {
    url = 'jdbc:mysql://127.0.0.1:3306/uid_generator?useSSL=false'
    user = 'root'
    password = 'my-secret-root-password'
}

test {
    useJUnitPlatform()
    maxHeapSize = "3g"

    doFirst() {
        dockerCompose.exposeAsEnvironment(test)
    }

    dependsOn(flywayClean, flywayMigrate, composeUp)
}

composeUp.doLast {
    flyway.url = "jdbc:mysql://127.0.0.1:${dockerCompose.servicesInfos.get("mysql").getTcpPorts().get(3306)}/uid_generator?useSSL=false"
    project.logger.info('Set flyway.url to: {}', flyway.url)
    flywayMigrate.dependsOn(composeUp)
    flywayClean.dependsOn(composeUp)
}
